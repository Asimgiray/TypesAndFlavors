plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.example.flavor_variant"
        minSdk 24
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("release_key")
            storePassword "releaseKey"
            keyAlias 'com.example.flavor_variant'
            keyPassword "releaseKey"
        }
        debug {
            storeFile file("debug_key")
            storePassword "android"
            keyAlias 'androiddebugkey'
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
            manifestPlaceholders = [buildTypeSuffix: "debug"]
        }
        release {
            minifyEnabled true
            debuggable false
            signingConfig signingConfigs.release
            applicationIdSuffix ".release"
            manifestPlaceholders = [buildTypeSuffix: "release"]
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        internal {
            initWith debug
            minifyEnabled true
        }
    }

    flavorDimensions "free"
    productFlavors {
        qa {
            applicationId "tr.com.flavor"
            resValue "string", "appName", '"Flavor Debug"'
            versionCode 10
            versionName "1.2.0"

            buildConfigField "String", "FLAVOR_TYPE", '"QA"'
            buildConfigField "String", "BASE_URL", '"www.qaEnv.com/"'
            buildConfigField "Boolean", "ENABLE_LOG", 'true'
        }
        preProd {
            applicationId "tr.com.flavor"
            resValue "string", "appName", '"Flavor QA"'
            versionCode 11
            versionName "1.2.2"

            buildConfigField "String", "FLAVOR_TYPE", '"PRE_PROD"'
            buildConfigField "String", "BASE_URL", '"www.prodEnv.com/"'
            buildConfigField "Boolean", "ENABLE_LOG", 'true'
        }
        prod {
            applicationId "tr.com.flavor"
            resValue "string", "appName", '"Flavor"'
            versionCode 11
            versionName "1.2.2"

            buildConfigField "String", "FLAVOR_TYPE", '"PRODUCTION"'
            buildConfigField "String", "BASE_URL", '"www.flavor.com/"'
            buildConfigField "Boolean", "ENABLE_LOG", 'false'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}